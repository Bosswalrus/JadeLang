program = global_definition ',' expression EOF;

global_definition = '(' global_definition_list ')'
                  | function_definition
                  | variable_definition
                  ;

global_definition_list = global_definition { ',' global_definition };
function_definition = identifier '(' [identifier_list] ')' ':=' expression;
variable_definition = identifier ':=' expression;

expression = '(' expression_list ')'
           | identifier
           | integer
           | function_call
           | unary_expression
           | binary_expression
           | labeled_expression
           | jump_expression
           | variable_definition
           | variable_assignment
           ;

expression_list = expression { ',' expression };
identifier = "[a-zA-Z_]([a-zA-Z0-9_]+)?";
identifier_list = identifier { ',' identifier };
integer = "[0-9]+";
function_call = identifier '(' expression_list ')';
unary_expression = unary_operator expression;
binary_expression = expression binary_operator expression;
labeled_expression = '#' '(' expression ',' expression ',' expression ')';
jump_expression = "#+!?";
variable_assignment = identifier '=' expression;

unary_operator = '+' | '-' | '~' | '!';
binary_operator = '+' | '-' | '*' | '/' | '%' | '|' | '&' | '^' | '<<' | '>>' | '==' | '!=' | '<' | '<=' | '>' | '>=' | '||' | '&&';
